group 'com.unplag'
version '1.0'

apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {

    def jerseyVersion = '2.24.1'

    compile 'org.glassfish.jersey.core:jersey-client:' + jerseyVersion
    compile 'org.glassfish.jersey.security:oauth1-client:' + jerseyVersion
    compile 'org.glassfish.jersey.media:jersey-media-multipart:' + jerseyVersion
    compile 'org.glassfish.jersey.media:jersey-media-json-jackson:' + jerseyVersion
    compile 'org.apache.commons:commons-lang3:3.5'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
}

configurations {
    itestCompile.extendsFrom testCompile
    itestCompile.extendsFrom testRuntime
}

sourceSets {
    itest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        resources.srcDir file('src/itest/resources')
    }
}


task itest(type: Test) {
    maxParallelForks = 1
    systemProperties = [
            key   : System.getProperty("key"),
            secret: System.getProperty("secret")
    ]

    testClassesDir = sourceSets.itest.output.classesDir
    classpath = sourceSets.itest.runtimeClasspath

    testLogging {
        events "passed", "skipped", "failed"
    }
}


task createPom {
    pom {
        project {
            groupId 'com.unplag'
            artifactId 'unplag-java-sdk'
            version project.version
            packaging 'jar'

            inceptionYear '2016'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }.writeTo("pom.xml")
}
